<!-- livebook:{"persist_outputs":true} -->

# 2022 Advent of Code - Day 3: Rucksack Reorganization

```elixir
Mix.install([:kino])
```

<!-- livebook:{"output":true} -->

```
:ok
```

## Links

* [Advent of Code](https://adventofcode.com/2022/day/3)
* [tylerj GitHub](https://github.com/tylerj/aoc/blob/main/2022/day-03.livemd)

## Input

```elixir
input = Kino.Input.textarea("Please paste your input file:")
```

```elixir
input = input |> Kino.Input.read()
```

<!-- livebook:{"output":true} -->

```
"dtddvvhwttHJhwdhJPddhwJGppmGjgpQgTjQplQpTljwpg\nBfzSzRSVVMVNRMDDNBSNSnfBmbrglGQbmNpQggFjpgpbQlQb\nZSBffLnVZdCCPJjhhL\nRGCZpWWWFlHQQbgvFssg\njLnMzjnrnjjNjhrjdwbHscsVVgDVQPvPwh\nnfJnLMLzjJMtnjNnnBbZtBWBqqbTTTBRpT\nnddlhBtqTBqTVSlBtmCmVcRVmZggfWbcZc\njDjvPrPSNPwrDNRWbbgWCjRRCcWm\nDzDwSpFrvrvFPQLzQnsqztBthTJnGJqlsJ\ngssGmzwgRgsNmTsqgDnDJnbDHHhhzFdDDh\nWQVFjMWrVQrVvVVjVctSSLSMZhnJZPBnbdnhbnHZZBDJBh\nVCtcccVQLrfvrSlGmfTfNgfmlFgm\nDsmfsBbNNZhDWsbmWmNbbPDHLFjcctjjGcnZGzncnctcGH\nSwVQJrjVwpgSVRpjpVRrlTMCFFCLCFFcHzzGMcHrtHHH\nppVwTwSwpwvSlSlJTjVVbPhsvvBssWsNfsqWPvWs\nBJwqwJtqqDDDrGDnPFzPFfpphD\nTgZscCHQLSHgZcfMzpjFFjzsshfj\nLcNlTVQCCVLLZTLNvpRtpvBBvRJmNB\nbDBGQBBCTTNPGPPwPzcHfVHrDtLWLVrWVjjHWr\ngpssqqsqlMFfLZQWftjVpr\nlvqqFMRlFcQvbzCNCG\nfhhMDdPhWMJMWvhhSfwRSGlzFbSFNlzw\nLcqTCqcgZqjTggVjcwbFRwbDBTzbRGRwTS\ncHLpZgnCHpQsDdsmQp\njwStJjJhtgJStpgwJMggQWqQTNTfNTWfbNNMCCNG\nzRZnFPRZPVncPGVFRlRmGHCTqfCCPCHHfLfbTQCbTq\nlnVmFZRZDnRVBFZcrZlhjpggvppthGhphpwprS\nlcttSptHHllQbMcsrltSQGpvNBzpgWBBBDDGWzvgLz\nPdjPVRFhFqFjRRCjzvRWnWLBLgbBBLzg\nhhCCFbPTmjPdhZjhPhZCmTjjMsrJSfHrcmHJrHHmlcJSsmft\nWhWnLZSSnSzQQhfLLNSfmDHrCFDDHtpjGGtTGQCG\ngJbJBcMVwJlRRdbwvwJBVtjdtHHTmptpHTCtTFrFCp\nJwwgvRMJlvJwgqgvqRMcnWWhLPzzsqfnZWnfWWnj\nzdwTSvzHMvVSzDCtZhtGmbTGhm\nlcBHfFjjgtsmDCgshD\ncJPBnqNFnLfHJFPqljclqJzQvSSVWvSnMwvSzSWWdMWM\npNJMcZrsZDLDcbcccMpQffHqvgdwdFFmdmqwvqmgmzsw\nhhnWjTTStRCGSMgvvgvdqvdFjvVz\nhCTSWhPGttTCGBWMRlTCMSnPBDJpbDfDpNpbbNJfJDJbpJpN\nlbcQcSNFchhQNqHLLqhLqrMpqM\nWfsnsszPWfBBVpHdprrpdnGL\nWTzWfwjtTBzwwBDzmfSSQmmbFZcpQNcbZZbv\nPwSJSlmtPPgwgmHhPPvRvGHLRLQRBQGCQVGb\nrnsFDnnfGGRWQRnW\ndfTTfTFdfrfFFFzQFPJPSSlhqtllNPzgwS\nMMbTFZrcrGZMDqNStWScDtzS\ndvWmhQggQvCnfnqPqDnDjnfP\nlgvdvLClWCQlgdhlrMBBHpGlwbHHGH\nCQHgQpPdCQpsCpzRwSVRSzRZwZ\nJbNBbcbrJvbJnqVznwwTzrzz\nwNbfLvvfDNNBHPFLhddFsQss\nVVzqvwzpqvzqNVVHGNqjHpNfSQDWdWwJdPWrWccdQrWrrDdd\nnLcbtBRtBhcnWSJQlJSgll\ntFbLLLRRhMtsBMtRCRsLCMBVjjvHTNjHHjzcvFFppGHzTT\nQCPrPWNPlWjGGZqGmvdPGd\nJgpHpSfphhfpVmBSgnTvdtddGvZVdvddDv\nLhphBfHpSwSwfHcMgfpmBWWWbsNCjFWsljNbbjlLjb\nQJmQbRmdfmdSQRQZSJltTltNvTrtDtrlftDD\nwpZcHVwwMgBpWMVgWpHLphztDvvGvDPlnGvDLlNrDPnNPl\nMcgWFWHHHzVpMgZQFqbjsdjqqRCq\nJPhLFfMJDLQnjNCvWWpdjjdM\ncrSwnwVnwSRBcNBNjjWCdC\nGlbTGbsSzrtbmbfhnJQP\nfDLSWVDRHHfVWHgPcZlDlZbbQhBcZQbb\njrmFmprTpFztmddjdjrpvBQlQZGhQbTsQbGcQbcbQs\nnvqdpmjFnwpLSWlfnVNnWl\nnZBRbBJzznNNCnJZwnBSCJMcpcTpcwhcqhmsmWMwFWLL\njQfvjgtfvPlHHqWpvWThpWqWch\nVljjjgjQjrTDlDgrHtVCbnJZzNzNbnRNNJZrJR\nMQtJnttlMLlJQsNhQrVVrFVWRRbbVFdJDD\nvGjvzmjzgHqSjjSzmSGHTWbfDFWrbFzFfdDVrfRWDb\nqPRqvTSPggqGgHCmllnCNLtnhcnnsnnw\nzrlZsQMFrsgQFMMjMCbjVDCTCW\nNqHNRdBppcJJcTpdmRfHThpdDWDtvbWVtbLjWbttWqqCCbLt\nmhJpJHTJmBhcJhwhgwzsQwSSlzQQzGlZ\nTvsszlvnzRRVTqzVrqrjjZGPfQPFqPqG\nmcNhDNchppWmWSNhdSmSCQNjPFjrfGjrgPFCrgFPgPgrLf\nSDddWpdMWSwNDmMNwlJRQwJlsVRRvzlsHt\nDTtggjsFFFTlPJhvctBqBqSRmSMBSRnmnRcm\nfGfwZdrbHVLdbGdHHwwQGVwBBCMMfvCNRNSMMMSRBmmRCN\ndGZzGHGVVbvHvHwbzpGbHLrwFDDFTtsglhFspgJshslTDJjT\nCbzspssWwCPcvvplrfqfDCJrDqdllB\nLjttnjNTNGgQQJdBrffTwB\nnVtLSgggjFwtMczhvzpZbSZW\nHCzCHHvWthWFHhssWCVmnqZrnqVrmrmgnbrqmN\nwPPGBjQQGwGbSlSLwgnpnrBZnBBmnMNnMN\njTTbJlJjPPLPGHHTthhhHcFWTT\nqRdvvPDrCpzPHzcdrrcRqtbJJgjhgtWjJgbWJtgCFb\nGTwGwNscLllGTZmGSTZGlSBMnhggjbgtgbtbsgWFFMhbMF\nZSQBSmlmzcrdQRqz\ncSpTRphwwghRfgSScqPpnDqDCjDjJJJJDvDLCvvn\nBVmmQFQBQVNBVmsWlbQFGBBlCHTJznzHLHvvCnjjNLHJDLHD\nMFsZZMbBGblbQTmQsFsQMMfPcPcwSpwtStgPphZtctPc\nQZbbZBdjPBjbQQbZnSSltlfwWvlvwNtNjwFMMN\nDLVqTJqpSVtfsptwfWpv\nrcRRVVTSbPQBPrBZ\ntjSgSjLFSnVjDWRsQj\nlcdqhfFpqZGpZqznrVRWPrnWRVBsVG\nFHddNNNHwTHMHvvS\nqCSDSQlwBHNbgJrHnLJH\nGRpRpRfnmRWWVWgVrF\njhdZjpnvGfTZZQPlCtqQQSsS\nFMZSGWWBrZjMBZMrBWMGjjZDnCRqpgPnbRwPbFnvvqFnDR\nQHcpfVVslfdVlQclcctqRgqgbsCwbCwPCCCPwD\nNLHfLhclmmhdfNNpfQMBmZWBrJMmZWBzMrjj\npBMpRgBMQwzRthmzLC\nHPcJvrvDbjvrFDcvWrHfHfWHmdddtdTLztmtdtfllmNdNhNz\nDvPFDvnPJLngQsggMGGQ\nBbcFHvbhhDbbTSvZmwwgJPPlDlZldd\nprCrNLMNgWWJBdrJ\nfQMLCfLLtpqsNNMnnfBhcBSVGbhhhcqVbcjc\nZchcZZjmmNpgmJtgmM\nRLrHllWrQZQGlBpbGFGFFM\nRQnLHrqPLnZHzqjfVPcvVTfCvPTC\nfMtwjfMwrbjfGrtrpPGrwpNNVNVqcbdVqHZTFNbcHSNL\nmgzvDnJmnJhFJHSTNqZLHncHLS\nvRzhzslJFhRffPPQMjGtGl\nVMMNjWppQVwzNWrZdrrtMCMZCtMT\nngDScLcvPPgDPDGhGDPGSHVbHTHmZtTSrBHZbZBmBb\nLhlglLghnVlplswJjs\nbGJQZZTQQLJJbQZlTZLjCGQTsDhWFhmshhvjWVFVVrgtDsst\nNScqwHcwwnnzBwqPqqsmVNhgsDDVtsghrFFg\npcrcwnpcffrcBzfbCR" <> ...
```

## Part 1: Prompt

One Elf has the important job of loading all of the [rucksacks](https://en.wikipedia.org/wiki/Backpack) with supplies for the jungle journey. Unfortunately, that Elf didn't quite follow the packing instructions, and so a few items now need to be rearranged.

Each rucksack has two large **compartments**. All items of a given type are meant to go into exactly one of the two compartments. The Elf that did the packing failed to follow this rule for exactly one item type per rucksack.

The Elves have made a list of all of the items currently in each rucksack (your puzzle input), but they need your help finding the errors. Every item type is identified by a single lowercase or uppercase letter (that is, a and A refer to different types of items).

The list of items for each rucksack is given as characters all on a single line. A given rucksack always has the same number of items in each of its two compartments, so the first half of the characters represent items in the first compartment, while the second half of the characters represent items in the second compartment.

For example, suppose you have the following list of contents from six rucksacks:

```
vJrwpWtwJgWrhcsFMMfFFhFp
jqHRNqRjqzjGDLGLrsFMfFZSrLrFZsSL
PmmdzqPrVvPwwTWBwg
wMqvLMZHhHMvwLHjbvcjnnSBnvTQFn
ttgJtRGJQctTZtZT
CrZsJsPPZsGzwwsLwLmpwMDw
```

* The first rucksack contains the items `vJrwpWtwJgWrhcsFMMfFFhFp`, which means its first compartment contains the items `vJrwpWtwJgWr`, while the second compartment contains the items `hcsFMMfFFhFp`. The only item type that appears in both compartments is lowercase `p`.
* The second rucksack's compartments contain `jqHRNqRjqzjGDLGL` and `rsFMfFZSrLrFZsSL`. The only item type that appears in both compartments is uppercase `L`.
* The third rucksack's compartments contain `PmmdzqPrV` and `vPwwTWBwg`; the only common item type is uppercase `P`.
* The fourth rucksack's compartments only share item type `v`.
* The fifth rucksack's compartments only share item type `t`.
* The sixth rucksack's compartments only share item type `s`.

To help prioritize item rearrangement, every item type can be converted to a **priority**:

* Lowercase item types a through z have priorities 1 through 26.
* Uppercase item types A through Z have priorities 27 through 52.

In the above example, the priority of the item type that appears in both compartments of each rucksack is 16 (p), 38 (L), 42 (P), 22 (v), 20 (t), and 19 (s); the sum of these is `157`.

Find the item type that appears in both compartments of each rucksack. **What is the sum of the priorities of those item types?**

To begin, [get your puzzle input](https://adventofcode.com/2022/day/3/input).

## Part 1: Solution

```elixir
defmodule Day03.Part1 do
  def call(input) when is_binary(input) do
    input
    |> String.split("\n")
    |> call()
  end

  def call(input) when is_list(input) do
    input
    |> Stream.map(&String.to_charlist/1)
    |> Stream.map(&Enum.split(&1, round(length(&1) / 2)))
    |> Stream.map(&find_common_char/1)
    |> Stream.map(&char_value/1)
    |> Enum.sum()
  end

  defp find_common_char({left, right}) do
    Enum.find(left, &Enum.member?(right, &1))
  end

  def char_value(x) when x in 97..122, do: x - 96
  def char_value(x) when x in 65..90, do: x - 38
end
```

<!-- livebook:{"output":true} -->

```
{:module, Day03.Part1, <<70, 79, 82, 49, 0, 0, 11, ...>>, {:char_value, 1}}
```

<!-- livebook:{"reevaluate_automatically":true} -->

```elixir
Day03.Part1.call(input)
```

<!-- livebook:{"output":true} -->

```
7848
```

### What is the sum of the priorities of those item types?

#### Your puzzle answer was `7848`.

## Part 2: Prompt

As you finish identifying the misplaced items, the Elves come to you with another issue.

For safety, the Elves are divided into groups of three. Every Elf carries a badge that identifies their group. For efficiency, within each group of three Elves, the badge is the **only item type carried by all three Elves**. That is, if a group's badge is item type B, then all three Elves will have item type B somewhere in their rucksack, and at most two of the Elves will be carrying any other item type.

The problem is that someone forgot to put this year's updated authenticity sticker on the badges. All of the badges need to be pulled out of the rucksacks so the new authenticity stickers can be attached.

Additionally, nobody wrote down which item type corresponds to each group's badges. The only way to tell which item type is the right one is by finding the one item type that is **common between all three Elves** in each group.

Every set of three lines in your list corresponds to a single group, but each group can have a different badge item type. So, in the above example, the first group's rucksacks are the first three lines:

```
vJrwpWtwJgWrhcsFMMfFFhFp
jqHRNqRjqzjGDLGLrsFMfFZSrLrFZsSL
PmmdzqPrVvPwwTWBwg
```

And the second group's rucksacks are the next three lines:

```
wMqvLMZHhHMvwLHjbvcjnnSBnvTQFn
ttgJtRGJQctTZtZT
CrZsJsPPZsGzwwsLwLmpwMDw
```

In the first group, the only item type that appears in all three rucksacks is lowercase r; this must be their badges. In the second group, their badge item type must be Z.

Priorities for these items must still be found to organize the sticker attachment efforts: here, they are 18 (r) for the first group and 52 (Z) for the second group. The sum of these is `70`.

Find the item type that corresponds to the badges of each three-Elf group. **What is the sum of the priorities of those item types?**

## Part 2: Solution

```elixir
defmodule Day03.Part2 do
  def call(input) when is_binary(input) do
    input
    |> String.split("\n")
    |> call()
  end

  def call(input) when is_list(input) do
    input
    |> Stream.map(&String.to_charlist/1)
    |> Stream.chunk_every(3)
    |> Stream.map(&process_group/1)
    |> Enum.sum()
  end

  def process_group([head | tail]) do
    tail
    |> Enum.reduce(
      MapSet.new(head),
      fn item, acc ->
        MapSet.intersection(acc, MapSet.new(item))
      end
    )
    |> Enum.at(0)
    |> char_value()
  end

  defdelegate char_value(x), to: Day03.Part1
end
```

<!-- livebook:{"output":true} -->

```
{:module, Day03.Part2, <<70, 79, 82, 49, 0, 0, 11, ...>>, [char_value: 1]}
```

<!-- livebook:{"reevaluate_automatically":true} -->

```elixir
Day03.Part2.call(input)
```

<!-- livebook:{"output":true} -->

```
2616
```

### What is the sum of the priorities of those item types?

#### Your puzzle answer was `2616`.

Both parts of this puzzle are complete! They provide two gold stars: **

At this point, you should [return to your Advent calendar](https://adventofcode.com/2022) and try another puzzle.

If you still want to see it, you can [get your puzzle input](https://adventofcode.com/2022/day/3/input).

## Tests

<!-- livebook:{"reevaluate_automatically":true} -->

```elixir
ExUnit.start(auto_run: false)

defmodule Day03Test do
  use ExUnit.Case, async: false

  setup_all do
    [
      input:
        "vJrwpWtwJgWrhcsFMMfFFhFp\njqHRNqRjqzjGDLGLrsFMfFZSrLrFZsSL\nPmmdzqPrVvPwwTWBwg\nwMqvLMZHhHMvwLHjbvcjnnSBnvTQFn\nttgJtRGJQctTZtZT\nCrZsJsPPZsGzwwsLwLmpwMDw"
    ]
  end

  describe "Part1.call/1" do
    test "returns expected value", %{input: input} do
      assert Day03.Part1.call(input) == 157
    end
  end

  describe "Part2.call/1" do
    test "returns expected value", %{input: input} do
      assert Day03.Part2.call(input) == 70
    end
  end
end

ExUnit.run()
```

<!-- livebook:{"output":true} -->

```
..
Finished in 0.00 seconds (0.00s async, 0.00s sync)
2 tests, 0 failures

Randomized with seed 783565
```

<!-- livebook:{"output":true} -->

```
%{excluded: 0, failures: 0, skipped: 0, total: 2}
```
