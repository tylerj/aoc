<!-- livebook:{"persist_outputs":true} -->

# Day 8 - Advent of Code 2020

```elixir
# Mix.install([:kino, :benchee])
```

## Links

* [Advent of Code (AoC)](https://adventofcode.com/2020/day/8)
* [AoC Puzzle Input](https://adventofcode.com/2020/day/8/input)
* [tylerj GitHub - Code](https://github.com/tylerj/aoc/blob/main/lib/advent_of_code/2020/day_08.ex)
* [tylerj GitHub - Livebook](https://github.com/tylerj/aoc/blob/main/lib/advent_of_code/2020/day-08.livemd)

## Prompt

### --- Day 8: Handheld Halting ---

Your flight to the major airline hub reaches cruising altitude without incident. While you consider checking the in-flight menu for one of those drinks that come with a little umbrella, you are interrupted by the kid sitting next to you.

Their [handheld game console](https://en.wikipedia.org/wiki/Handheld_game_console) won't turn on! They ask if you can take a look.

You narrow the problem down to a strange **infinite loop** in the boot code (your puzzle input) of the device. You should be able to fix it, but first you need to be able to run the code in isolation.

The boot code is represented as a text file with one **instruction** per line of text. Each instruction consists of an **operation** (`acc`, `jmp`, or `nop`) and an **argument** (a signed number like `+4` or `-20`).

* `acc` increases or decreases a single global value called the **accumulator** by the value given in the argument. For example, `acc +7` would increase the accumulator by 7. The accumulator starts at 0. After an acc instruction, the instruction immediately below it is executed next.
* `jmp` **jumps** to a new instruction relative to itself. The next instruction to execute is found using the argument as an offset from the jmp instruction; for example, `jmp +2` would skip the next instruction, `jmp +1` would continue to the instruction immediately below it, and `jmp -20` would cause the instruction 20 lines above to be executed next.
* `nop` stands for `No OPeration` - it does nothing. The instruction immediately below it is executed next.

For example, consider the following program:

```
nop +0
acc +1
jmp +4
acc +3
jmp -3
acc -99
acc +1
jmp -4
acc +6
```

These instructions are visited in this order:

```
nop +0  | 1
acc +1  | 2, 8(!)
jmp +4  | 3
acc +3  | 6
jmp -3  | 7
acc -99 |
acc +1  | 4
jmp -4  | 5
acc +6  |
```

First, the `nop +0` does nothing. Then, the accumulator is increased from 0 to 1 (`acc +1`) and `jmp +4` sets the next instruction to the other `acc +1` near the bottom. After it increases the accumulator from 1 to 2, `jmp -4` executes, setting the next instruction to the only `acc +3`. It sets the accumulator to 5, and `jmp -3` causes the program to continue back at the first `acc +1`.

This is an **infinite loop**: with this sequence of jumps, the program will run forever. The moment the program tries to run any instruction a second time, you know it will never terminate.

Immediately **before** the program would run an instruction a second time, the value in the accumulator is 5.

Run your copy of the boot code. Immediately before any instruction is executed a second time, **what value is in the accumulator?**

To begin, [get your puzzle input](https://adventofcode.com/2020/day/8/input).

<!-- livebook:{"break_markdown":true} -->

### --- Part Two ---

After some careful analysis, you believe that **exactly one instruction is corrupted**.

Somewhere in the program, **either** a `jmp` is supposed to be a `nop`, or a `nop` is supposed to be a `jmp`. (No `acc` instructions were harmed in the corruption of this boot code.)

The program is supposed to terminate by **attempting to execute an instruction immediately after the last instruction in the file**. By changing exactly one `jmp` or `nop`, you can repair the boot code and make it terminate correctly.

For example, consider the same program from above:

```
nop +0
acc +1
jmp +4
acc +3
jmp -3
acc -99
acc +1
jmp -4
acc +6
```

If you change the first instruction from `nop +0` to `jmp +0`, it would create a single-instruction infinite loop, never leaving that instruction. If you change almost any of the `jmp` instructions, the program will still eventually find another `jmp` instruction and loop forever.

However, if you change the second-to-last instruction (from `jmp -4` to `nop -4`), the program terminates! The instructions are visited in this order:

```
nop +0  | 1
acc +1  | 2
jmp +4  | 3
acc +3  |
jmp -3  |
acc -99 |
acc +1  | 4
nop -4  | 5
acc +6  | 6
```

After the last instruction (`acc +6`), the program terminates by attempting to run the instruction below the last instruction in the file. With this change, after the program terminates, the accumulator contains the value **8** (`acc +1`, `acc +1`, `acc +6`).

Fix the program so that it terminates normally by changing exactly one `jmp` (to `nop`) or `nop` (to `jmp`). **What is the value of the accumulator after the program terminates?**

Although it hasn't changed, you can still [get your puzzle input](https://adventofcode.com/2020/day/8/input).

## Input

```elixir
input = Kino.Input.textarea("Please paste your input file:")
```

<!-- livebook:{"reevaluate_automatically":true} -->

```elixir
input = input |> Kino.Input.read()
input = AdventOfCode.Input.get!(8, 2020)
```

<!-- livebook:{"output":true} -->

```
"acc +22\nacc +0\njmp +1\nacc +49\njmp +203\njmp +545\nacc +26\njmp +326\nacc +34\nacc +23\nnop +93\njmp +346\nnop +513\nacc -5\njmp +413\njmp +560\nacc -1\njmp +344\nacc +44\nacc -14\nnop +570\nacc +12\njmp +137\njmp +411\njmp +1\njmp +128\nacc -4\nacc +28\nacc +42\njmp +206\njmp -7\nnop +386\nacc +9\njmp +280\njmp +63\nacc +39\nacc +13\nacc +30\nacc +19\njmp +25\njmp -9\nacc +43\njmp +180\nacc -16\nacc -8\nacc +17\nacc +11\njmp +550\nacc +29\nacc +40\njmp -44\njmp -10\nnop +425\nacc -12\njmp +19\nacc +38\nacc +1\njmp -9\njmp +317\nacc +46\nacc -15\nacc +10\nacc -1\njmp +382\nacc +3\nacc +7\nnop +126\njmp +378\nacc +48\njmp -21\njmp +547\nacc +28\njmp +266\nacc -15\nacc +11\nacc +11\njmp +499\nacc +5\nacc +38\nacc +17\nacc -7\njmp +444\nnop +357\nacc +14\nacc +8\nacc +1\njmp +264\nnop +37\nacc +15\nacc +4\njmp +372\nacc -1\njmp +416\nacc +42\nacc +44\nnop +58\njmp +494\nacc +24\nacc +8\njmp +158\nacc +19\nnop +384\njmp +43\nacc +0\njmp +27\njmp +479\nacc +37\njmp +332\nacc -5\nacc +49\njmp -87\nacc -2\nacc +41\njmp +50\nacc -7\nacc +8\nacc -8\nacc +3\njmp +68\njmp +1\nacc -11\nnop +117\njmp +403\njmp +348\njmp -33\njmp +1\nacc +20\njmp +300\njmp +148\njmp +1\njmp +361\nacc +1\nacc +12\nacc +42\njmp -111\nacc +36\nacc +1\nacc +18\njmp -10\njmp +20\njmp +464\nnop -89\nnop +152\njmp +2\njmp +48\nacc +17\nacc +29\nnop +100\nnop -96\njmp +27\nacc -3\nacc +18\njmp +293\njmp +222\nacc -19\nacc +35\nacc +46\nacc +3\njmp +230\nnop -147\nacc +39\njmp +46\njmp +123\nacc +23\nacc -9\nacc +33\nacc +30\njmp +444\nacc -8\nnop +188\nacc +24\njmp -113\njmp -156\nacc -10\nacc +30\njmp +24\nacc +49\nacc +33\nnop -75\nacc -14\njmp -52\nacc +33\njmp -156\njmp +401\nacc -9\njmp +14\nacc +37\nacc +4\njmp +37\nacc +29\nnop +57\njmp +243\nacc +10\nnop +382\nacc +19\nacc +13\njmp +216\nacc +17\njmp +177\nnop +405\nnop +9\nacc +43\njmp +30\nnop +387\njmp -51\njmp +97\njmp +348\njmp +397\njmp +219\nnop +148\nacc +34\njmp -12\nacc -16\nacc +5\nacc +33\njmp +29\nacc +49\njmp +126\nacc +19\nacc -11\nacc -11\njmp +333\nacc +10\njmp -14\njmp +89\nacc +0\nacc +11\njmp -196\nacc +33\njmp +1\nacc +31\njmp +353\njmp +268\nnop +170\njmp +218\njmp +90\nacc -18\njmp -45\njmp -156\njmp -227\nacc +5\nacc -13\njmp -136\njmp +1\njmp -52\nacc +24\njmp +104\nnop -3\nacc +16\nacc +0\nacc +50\njmp -7\nacc +37\nacc +7\nacc -19\nacc -14\njmp +171\nacc +12\nacc +42\nacc -15\njmp +12\nacc +21\nacc +37\njmp -56\njmp +1\nacc -3\njmp -147\nnop -84\nacc -14\nacc +19\nnop +221\njmp -132\nacc +10\njmp +27\nacc +0\njmp +250\nacc +12\nacc -9\nacc +5\nnop +263\njmp +30\njmp +1\nacc +10\nacc -17\njmp -27\nacc +5\nacc +40\nacc -12\nacc -7\njmp +99\nacc +45\nacc +3\nacc +39\njmp -229\nacc +50\nacc +17\nacc +31\njmp -12\nnop -41\njmp +89\njmp -36\njmp +49\njmp +1\nnop +214\nacc +25\nacc +23\njmp +211\nnop +180\nacc +45\njmp +245\nacc -10\njmp +225\njmp -120\nacc -4\nacc +45\njmp +214\nacc +6\nacc +50\nacc +26\njmp -180\nnop +83\njmp +91\nacc +37\nacc +42\njmp -115\njmp +146\nacc +31\njmp -144\nacc -14\njmp -238\nacc +43\nacc +31\njmp -149\nacc -19\njmp +157\nacc -8\nacc -16\njmp +274\nacc +21\nacc -14\njmp -135\nacc +40\njmp -272\nacc +33\nacc -11\njmp -51\nacc +35\nacc +31\nacc +14\njmp -267\nacc +38\nacc -16\nacc +43\njmp -25\nacc +37\nnop +40\njmp +219\nacc +23\nnop -166\njmp +126\njmp -241\nacc +37\nacc +39\nnop -187\nacc +21\njmp -179\nacc +32\njmp +72\nacc +14\nacc +0\nacc +12\nacc +22\njmp -15\nnop -30\njmp -339\nacc +21\njmp -160\nacc +14\nacc +17\nacc -18\nnop +210\njmp +110\nacc +46\njmp -325\nacc +27\nacc -13\nacc -4\njmp -259\nacc -19\nacc -11\nacc +19\nacc +36\njmp -357\nnop -60\njmp +190\nacc +34\nacc -4\nnop +20\njmp +1\njmp -152\nacc +35\nacc -18\njmp -77\nnop -264\nacc -2\nacc +4\nacc +4\njmp -224\nnop -75\nacc +6\nacc -14\njmp -270\nacc -14\njmp -365\nacc +23\nacc -19\njmp +61\nacc -1\nacc +7\nacc +0\nacc +11\njmp +176\nacc +17\nacc -5\nacc +12\nacc +38\njmp +45\njmp +1\nacc +22\nacc -11\nacc +10\njmp -396\nacc +36\njmp -280\nacc +23\nnop +56\nacc -7\njmp -421\njmp -77\nacc +31\nnop -97\nacc +29\njmp -401\nnop -324\njmp -237\nacc +24\nacc +6\nacc -9\njmp -337\nacc -7\nacc -3\njmp -445\nacc +24\nacc +11\nacc +47\nacc +47\njmp -359\nacc -6\nacc -6\njmp +1\njmp +1\njmp -34\nacc -12\nacc +34\nacc +36\nacc +3\njmp +11\nacc -18\nacc +26\nacc +43\njmp -454\nacc +6\nacc +46\nacc +45\nacc +37\njmp -23\njmp -412\nacc +31\nacc +2\nacc -9\nacc +24\njmp -469\nnop -114\nacc -19\njmp -127\njmp -313\njmp -367\nacc +0\njmp +34\nacc +22\njmp -152\nacc +18\nacc +14\nacc +43\njmp +56\nnop -61\nacc -14\nacc +22\nnop -71\njmp -408\nnop -359\nacc -15\nacc +14\nacc +5\njmp -266\nacc -1" <> ...
```

## Solution

```elixir
defmodule Day08 do
  defdelegate parse(input), to: __MODULE__.Input

  def part1(input) do
    input
    |> parse()
    |> run()
    |> then(fn {:looped, acc} -> acc end)
  end

  def part2(input) do
    instr = parse(input)

    instr
    |> Enum.with_index()
    |> Enum.filter(fn
      {{"acc", _}, _} -> false
      {{"nop", _}, _} -> true
      {{"jmp", _}, _} -> true
    end)
    |> Enum.find_value(fn {_, i} ->
      instr
      |> List.update_at(i, fn
        {"nop", x} -> {"jmp", x}
        {"jmp", x} -> {"nop", x}
      end)
      |> run()
      |> then(fn
        {:ok, acc} -> acc
        {:looped, _} -> nil
      end)
    end)
  end

  defp run(instr), do: run(instr, length(instr), 0, [], 0)

  defp run(instr, size, index, seen, acc) do
    cond do
      index == size ->
        {:ok, acc}

      index in seen ->
        {:looped, acc}

      true ->
        {new_index, new_acc} = Enum.at(instr, index) |> process(index, acc)
        run(instr, size, new_index, [index | seen], new_acc)
    end
  end

  defp process({"acc", x}, index, acc), do: {index + 1, acc + x}
  defp process({"jmp", x}, index, acc), do: {index + x, acc}
  defp process({"nop", _}, index, acc), do: {index + 1, acc}

  defmodule Input do
    def parse(input) when is_binary(input) do
      input
      |> String.splitter("\n", trim: true)
      |> parse()
    end

    def parse(input) do
      Enum.map(input, &parse_line/1)
    end

    def parse_line(line) do
      [op, x] = String.split(line, " ")

      {op, String.to_integer(x)}
    end
  end
end
```

<!-- livebook:{"output":true} -->

```
{:module, Day08, <<70, 79, 82, 49, 0, 0, 16, ...>>,
 {:module, Day08.Input, <<70, 79, 82, ...>>, {:parse_line, 1}}}
```

**Run your copy of the boot code. Immediately before any instruction is executed a second time, what value is in the accumulator?**

Your puzzle answer was `1930`.

<!-- livebook:{"reevaluate_automatically":true} -->

```elixir
Day08.part1(input)
```

<!-- livebook:{"output":true} -->

```
1930
```

**Fix the program so that it terminates normally by changing exactly one `jmp` (to `nop`) or `nop` (to `jmp`). What is the value of the accumulator after the program terminates?**

Your puzzle answer was `1688`.

<!-- livebook:{"reevaluate_automatically":true} -->

```elixir
Day08.part2(input)
```

<!-- livebook:{"output":true} -->

```
1688
```

Both parts of this puzzle are complete! They provide two gold stars: **

At this point, you should [return to your Advent calendar](https://adventofcode.com/2020) and try another puzzle.

If you still want to see it, you can [get your puzzle input](https://adventofcode.com/2020/day/8/input).
