<!-- livebook:{"persist_outputs":true} -->

# Day 2 - Advent of Code 2022

```elixir
Mix.install([:kino])
```

<!-- livebook:{"output":true} -->

```
:ok
```

## Links

* [Advent of Code (AoC)](https://adventofcode.com/2022/day/2)
* [AoC Puzzle Input](https://adventofcode.com/2022/day/2/input)
* [tylerj GitHub - Code](https://github.com/tylerj/aoc/blob/main/lib/advent_of_code/2022/day_02.ex)
* [tylerj GitHub - Livebook](https://github.com/tylerj/aoc/blob/main/lib/advent_of_code/2022/day-02.livemd)

## Part 1: Prompt

The Elves begin to set up camp on the beach. To decide whose tent gets to be closest to the snack storage, a giant [Rock Paper Scissors](https://en.wikipedia.org/wiki/Rock_paper_scissors) tournament is already in progress.

Rock Paper Scissors is a game between two players. Each game contains many rounds; in each round, the players each simultaneously choose one of Rock, Paper, or Scissors using a hand shape. Then, a winner for that round is selected: Rock defeats Scissors, Scissors defeats Paper, and Paper defeats Rock. If both players choose the same shape, the round instead ends in a draw.

Appreciative of your help yesterday, one Elf gives you an **encrypted strategy guide** (your puzzle input) that they say will be sure to help you win. "The first column is what your opponent is going to play: A for Rock, B for Paper, and C for Scissors. The second column--" Suddenly, the Elf is called away to help with someone's tent.

The second column, you reason, must be what you should play in response: X for Rock, Y for Paper, and Z for Scissors. Winning every time would be suspicious, so the responses must have been carefully chosen.

The winner of the whole tournament is the player with the highest score. Your **total score** is the sum of your scores for each round. The score for a single round is the score for the **shape you selected** (1 for Rock, 2 for Paper, and 3 for Scissors) plus the score for the **outcome of the round** (0 if you lost, 3 if the round was a draw, and 6 if you won).

Since you can't be sure if the Elf is trying to help you or trick you, you should calculate the score you would get if you were to follow the strategy guide.

For example, suppose you were given the following strategy guide:

```
A Y
B X
C Z
```

This strategy guide predicts and recommends the following:

* In the first round, your opponent will choose Rock (A), and you should choose Paper (Y). This ends in a win for you with a score of `8` (2 because you chose Paper + 6 because you won).
* In the second round, your opponent will choose Paper (B), and you should choose Rock (X). This ends in a loss for you with a score of `1` (1 + 0).
* The third round is a draw with both players choosing Scissors, giving you a score of 3 + 3 = `6`.

In this example, if you were to follow the strategy guide, you would get a total score of `15` (8 + 1 + 6).

### What would your total score be if everything goes exactly according to your strategy guide?

To begin, [get your puzzle input](https://adventofcode.com/2022/day/2/input).

## Part 1: Solution

```elixir
input = Kino.Input.textarea("Please paste your input file:")
```

```elixir
input_str = input |> Kino.Input.read()
```

<!-- livebook:{"output":true} -->

```
"B Z\nC Z\nB X\nA Y\nB X\nB X\nA X\nB Z\nC Z\nB Y\nA Z\nC X\nB X\nC X\nB Z\nB Z\nC Y\nB Z\nB Z\nC Z\nB Z\nB Y\nB X\nB Y\nC Z\nC Y\nC Z\nA X\nC Z\nB X\nC Z\nB Y\nB X\nA Y\nA X\nA Y\nB Y\nB X\nB X\nA Z\nB Z\nB Y\nC Z\nB X\nC Y\nB Z\nB Y\nC Y\nA X\nA Y\nC Y\nC Z\nB Z\nB X\nC Z\nA X\nB X\nA Y\nB Z\nC Y\nA Y\nC Z\nC Z\nA X\nB X\nC Z\nA Z\nA Z\nB X\nB X\nB X\nA Y\nB X\nB X\nC Y\nB X\nC Z\nC Y\nB Z\nA X\nB X\nB X\nA X\nC Y\nC Y\nA X\nA X\nB Z\nB X\nC Z\nB X\nB Z\nA Z\nB Z\nA X\nA X\nB Z\nA X\nB X\nB X\nB X\nA Y\nA Y\nA Y\nB X\nC Y\nB Z\nA Y\nB X\nA Z\nC X\nA Z\nB Y\nB Z\nC Z\nB Z\nA Y\nB X\nB Z\nB Z\nB Z\nC Y\nB X\nA Y\nB Z\nB Y\nB Z\nB X\nA X\nA X\nB Y\nB X\nC Y\nA Y\nA Z\nB Z\nB Z\nB Y\nB X\nB Z\nB X\nB Z\nB Z\nB X\nB Z\nB Z\nB Z\nB X\nA Y\nB X\nB Z\nA X\nB Z\nB Z\nB X\nC Y\nA Z\nB Z\nC Z\nB X\nA Z\nB X\nA Z\nC Y\nC Y\nA Y\nA Y\nB Z\nA Y\nA Y\nC Z\nA X\nB X\nB X\nC Y\nA Z\nB Y\nC Y\nB Z\nB Y\nB Z\nA X\nB Z\nC Z\nB X\nB Y\nA X\nC Z\nB Y\nB Z\nB Z\nA Z\nB X\nA Y\nC Y\nC Y\nB Z\nB Z\nB X\nB Z\nB Z\nB Y\nB Z\nB Z\nB Z\nB X\nB X\nB Z\nB Y\nB Z\nC Y\nA Z\nA Y\nB X\nA X\nB Z\nB Z\nA Z\nB Z\nB Z\nB X\nA Y\nC Z\nC Y\nB Z\nB Z\nC Z\nA X\nB Z\nB Z\nB X\nA Y\nA Z\nB Z\nC Y\nC Z\nA Y\nB Z\nB X\nC Z\nA X\nC Z\nB Z\nC Z\nB X\nC Y\nB X\nB Z\nB X\nA Y\nA Z\nB Z\nB X\nB Z\nC Z\nC Z\nC Y\nB X\nB Y\nB Z\nC Z\nC Z\nB Z\nB X\nB Z\nC Y\nA Y\nC Y\nB X\nC Y\nB Y\nC X\nB X\nA Y\nC Z\nB X\nB Z\nB Y\nB X\nB X\nA Z\nB Z\nC Z\nB X\nB X\nA X\nB X\nB X\nC X\nC Y\nA X\nB X\nB Z\nB X\nA Y\nB X\nB Y\nB X\nB X\nB Y\nB X\nA Z\nB X\nB Z\nB X\nC Z\nA Z\nC X\nB Z\nA Y\nB X\nB X\nA Z\nA Y\nB Z\nB Z\nA Y\nC Z\nB X\nB X\nC Z\nB Z\nB Z\nB Z\nA Y\nA Y\nA X\nB X\nC X\nB X\nB Z\nB X\nA X\nB X\nB Z\nB X\nA Y\nB X\nC Y\nB X\nB Z\nB Z\nC Z\nC Z\nC Y\nB Z\nB X\nB Z\nA Y\nC Y\nB X\nC Y\nC Z\nA X\nB Z\nA X\nB X\nC Y\nA X\nA Y\nB X\nA Y\nA Z\nC X\nB X\nB Y\nB Z\nB X\nA Y\nB Z\nB X\nA X\nB X\nB X\nB Y\nB Z\nB Z\nB X\nB X\nA X\nB Z\nB Z\nB Z\nA Y\nC Z\nA X\nA Y\nB Z\nB X\nC Y\nA X\nB X\nB Z\nB Y\nA Y\nB Z\nB Z\nB X\nA Y\nA Y\nA Y\nC Y\nB X\nB X\nB Z\nB Z\nA Z\nA Y\nB Z\nB X\nB X\nB X\nA X\nB X\nC Y\nB Z\nA Y\nB X\nA Y\nA X\nC Y\nB Z\nC Z\nB Z\nB X\nB Z\nA Y\nB Z\nC Z\nB Z\nB Z\nA X\nB Z\nB Z\nB X\nB Z\nB Z\nB Z\nC X\nB X\nB X\nA Y\nA Y\nB X\nB X\nC Z\nB X\nB X\nC Y\nC Y\nC Z\nB X\nB Z\nB Z\nB Z\nA Y\nA Y\nA X\nC Z\nA Z\nA X\nB Z\nC Z\nA X\nB X\nB Y\nC Z\nB Y\nB Z\nC Y\nC Y\nC Y\nB Z\nB Z\nB X\nB Z\nB Y\nB X\nB Z\nA X\nB X\nA Y\nA X\nB Z\nA Y\nC Z\nC X\nB X\nB X\nC Y\nB Z\nA Y\nB Z\nB X\nC X\nB Z\nA X\nA Y\nA Y\nC Y\nC Z\nC Z\nB X\nA X\nB X\nB X\nB Z\nC Y\nB X\nC Z\nC Y\nB X\nB Z\nB X\nB X\nC Z\nA Y\nB Z\nB X\nA X\nB Z\nB Z\nB X\nB X\nC Z\nC Z\nB Y\nC Z\nB Z\nC Y\nB X\nB X\nC Y\nB X\nB X\nA Z\nB X\nB X\nA Y\nB X\nB X\nB X\nB Z\nB Z\nC Z\nA Y\nB X\nB Z\nB X\nB Z\nB Z\nB X\nB Y\nC Z\nA Z\nA Y\nC Y\nB X\nA X\nB Y\nA X\nB Y\nA Y\nC X\nB X\nA Y\nB Z\nB Z\nB X\nB Z\nB Z\nB X\nB X\nA Y\nB Z\nA Y\nB Y\nB Y\nB Y\nB X\nB Z\nB Z\nA Y\nA X\nC Y\nB X\nB X\nA Y\nB X\nA Y\nB Z\nB Z\nB Y\nB Z\nC Z\nC Z\nC Y\nB Z\nC Y\nA X\nB X\nC Y\nC Z\nB X\nC Y\nA Y\nB Z\nB Z\nA X\nC Y\nB X\nA Y\nC Z\nB Z\nB X\nA Y\nC Z\nA X\nA Y\nC X\nC Y\nA Y\nB Z\nB X\nA Z\nB Z\nB Z\nB X\nC Z\nB X\nB Z\nB X\nB X\nB Z\nB X\nA X\nA Y\nB Y\nA Y\nA Y\nB Z\nC Z\nB X\nB Z\nA Y\nA Y\nA Z\nB X\nA Y\nA Y\nB X\nA X\nB Y\nB Z\nB Y\nA Z\nC Y\nB Z\nA X\nA Z\nC Z\nB X\nB Z\nB X\nC Y\nA X\nB Z\nA Y\nB X\nB Z\nB X\nC Y\nB Z\nB X\nA Y\nB X\nC Y\nC X\nA X\nB Z\nB X\nA X\nA Z\nB X\nB Y\nA Z\nB Z\nC Y\nA Y\nC X\nB Z\nA Y\nC Y\nC Z\nB Y\nC Y\nA Z\nB Z\nB Z\nA Y\nB X\nC X\nA Y\nA Y\nA Z\nB X\nB X\nB Y\nA X\nB X\nB Z\nB Z\nB X\nB Y\nC Z\nC Y\nA Y\nA Y\nC X\nC Z\nC Y\nC Y\nA Y\nA Y\nB Z\nC Y\nC Y\nA Y\nA Y\nC Z\nB Z\nA X\nB Y\nB Z\nB Z\nB Z\nB Z\nB X\nA Y\nA Y\nB Z\nA Y\nC X\nA X\nC Z\nB Z\nB Z\nA X\nC Y\nB Z\nB X\nB X\nB X\nB Z\nC Y\nB Z\nB X\nB X\nB Z\nC Y\nB X\nA Z\nB X\nB Z\nA X\nC X\nA X\nB Z\nB Z\nB Z\nB Z\nA Y\nB Z\nB Y\nB Z\nB Z\nB X\nB Z\nB Z\nA Y\nA X\nB X\nA Z\nB Z\nA Z\nB X\nB X\nB Z\nA Y\nB Y\nA X\nB X\nB X\nA Y\nB X\nB X\nB Z\nB Y\nB Z\nC Z\nB X\nC Y\nB Z\nB Z\nC Y\nB Z\nB Z\nB Z\nB Z\nB Z\nC Y\nA Y\nA X\nB Z\nC Y\nB Y\nC Z\nB Z\nB Z\nC Z\nB Z\nB Z\nB X\nA Y\nB Z\nB Z\nC Y\nA Y\nB Z\nA Y\nB Z\nC Z\nC Y\nA Y\nA Z\nB X\nA X\nB Z\nB Y\nA X\nA X\nA Y\nB X\nC Y\nB X\nB Z\nA Y\nB X\nB X\nA Y\nA Z\nC Z\nC Y\nA Y\nB X\nB Z\nB X\nC Z\nA Y\nB X\nA Y\nB Z\nB X\nC Y\nA Y\nB Z\nC Y\nB Y\nB X\nC Y\nA Y\nB Z\nC Y\nB X\nA X\nB Z\nB Z\nC Y\nA Y\nB Z\nC Y\nB X\nA X\nA Z\nC X\nB Z\nB Z\nC Y\nB Y\nC Z\nC Y\nA X\nA Y\nA X\nA Y\nC Z\nC Y\nC Z\nC Z\nC Y\nA X\nC Z\nB X\nC X\nB X\nA Z\nB X\nC Z\nA Y\nA X\nA Z\nC Z\nB X\nC Y\nA Y\nC Y\nC Z\nC Y\nC Y\nC X\nB Z\nB X\nB Y\nA X\nB Z\nB Y\nC Y\nC Y\nC Z\nA Z\nA X\nA Y\nC Z\nB Z\nB X\nB Z\nB Z\nB X\nB Z\nC Y\nA X\nB X\nA Z\nB X\nC Y\nB Z\nB X\nB Z\nC Z\nC Z\nA X\nB Z\nB X\nA Y\nB Z\nA Y\nB Y\nC Z\nC Y\nA X\nA Y\nC Y\nC Z\nB X\nC Y\nB Z\nB Z\nB Z\nC Z\nB X\nC Y\nB Z\nC Z\nB X\nA Y\nA X\nB Z\nB Z\nC Y\nB X\nB Z\nC Y\nA Y\nC Y\nA Y\nB X\nC Z\nA X\nA Y\nC Y\nC Z\nB Z\nB Z\nB Z\nA Y\nA Y\nC Z\nA Z\nB X\nA X\nB Z\nC Z\nB X\nC Y\nB Z\nB X\nB Z\nB Z\nB X\nC Z\nB X\nB Z\nB X\nA X\nB X\nA X\nB Z\nB Z\nA Y\nB X\nB Z\nB Z\nC Z\nC Y\n" <> ...
```

```elixir
defmodule Day02.Part1 do
  @shape_values %{
    :rock => 1,
    :paper => 2,
    :scissors => 3
  }

  @shape_map %{
    "A" => :rock,
    "B" => :paper,
    "C" => :scissors,
    "X" => :rock,
    "Y" => :paper,
    "Z" => :scissors
  }

  @outcome_values %{
    win: 6,
    lose: 0,
    draw: 3
  }

  def score_lines(input) when is_binary(input) do
    input
    |> String.split("\n")
    |> score_lines()
  end

  def score_lines(input) when is_list(input) do
    Enum.map(input, &score_line/1)
  end

  def score_line(line) do
    line
    |> String.split(" ")
    |> calculate_score()
  end

  def calculate_score([them, me]) when is_binary(them) and is_binary(me) do
    calculate_score(@shape_map[them], @shape_map[me])
  end

  def calculate_score(them, me) when is_atom(them) and is_atom(me) do
    @outcome_values[win_lose_draw(them, me)] + @shape_values[me]
  end

  defp win_lose_draw(a, a), do: :draw
  defp win_lose_draw(:rock, :paper), do: :win
  defp win_lose_draw(:rock, :scissors), do: :lose
  defp win_lose_draw(:paper, :scissors), do: :win
  defp win_lose_draw(:paper, :rock), do: :lose
  defp win_lose_draw(:scissors, :rock), do: :win
  defp win_lose_draw(:scissors, :paper), do: :lose
end
```

<!-- livebook:{"output":true} -->

```
{:module, Day02.Part1, <<70, 79, 82, 49, 0, 0, 12, ...>>, {:win_lose_draw, 2}}
```

<!-- livebook:{"reevaluate_automatically":true} -->

```elixir
input_str |> Day02.Part1.score_lines() |> Enum.sum()
```

<!-- livebook:{"output":true} -->

```
12458
```

### What would your total score be if everything goes exactly according to your strategy guide?

#### Your puzzle answer was `12458`.

## Part 2: Prompt

The Elf finishes helping with the tent and sneaks back over to you. "Anyway, the second column says how the round needs to end: X means you need to lose, Y means you need to end the round in a draw, and Z means you need to win. Good luck!"

The total score is still calculated in the same way, but now you need to figure out what shape to choose so the round ends as indicated. The example above now goes like this:

* In the first round, your opponent will choose Rock (A), and you need the round to end in a draw (Y), so you also choose Rock. This gives you a score of 1 + 3 = `4`.
* In the second round, your opponent will choose Paper (B), and you choose Rock so you lose (X) with a score of 1 + 0 = `1`.
* In the third round, you will defeat your opponent's Scissors with Rock for a score of 1 + 6 = `7`.

Now that you're correctly decrypting the ultra top secret strategy guide, you would get a total score of `12`.

Following the Elf's instructions for the second column, **what would your total score be if everything goes exactly according to your strategy guide?**

## Part 2: Solution

```elixir
defmodule Day02.Part2 do
  @shape_values %{
    :rock => 1,
    :paper => 2,
    :scissors => 3
  }

  @shapes %{
    "A" => :rock,
    "B" => :paper,
    "C" => :scissors
  }

  @outcomes %{
    "X" => :lose,
    "Y" => :draw,
    "Z" => :win
  }

  @outcome_values %{
    win: 6,
    lose: 0,
    draw: 3
  }

  def score_lines(input) when is_binary(input) do
    input
    |> String.split("\n")
    |> score_lines()
  end

  def score_lines(input) when is_list(input) do
    Enum.map(input, &score_line/1)
  end

  def score_line(line) do
    line
    |> String.split(" ")
    |> calculate_score()
  end

  def calculate_score([them, outcome]) when is_binary(them) and is_binary(outcome) do
    calculate_score(@shapes[them], @outcomes[outcome])
  end

  def calculate_score(them, outcome) do
    @outcome_values[outcome] + @shape_values[my_shape(them, outcome)]
  end

  defp my_shape(them_shape, :draw), do: them_shape
  defp my_shape(:rock, :win), do: :paper
  defp my_shape(:rock, :lose), do: :scissors
  defp my_shape(:paper, :win), do: :scissors
  defp my_shape(:paper, :lose), do: :rock
  defp my_shape(:scissors, :win), do: :rock
  defp my_shape(:scissors, :lose), do: :paper
end
```

<!-- livebook:{"output":true} -->

```
{:module, Day02.Part2, <<70, 79, 82, 49, 0, 0, 12, ...>>, {:my_shape, 2}}
```

<!-- livebook:{"reevaluate_automatically":true} -->

```elixir
input_str |> Day02.Part2.score_lines() |> Enum.sum()
```

<!-- livebook:{"output":true} -->

```
12683
```

### what would your total score be if everything goes exactly according to your strategy guide?

#### Your puzzle answer was `12683`.

Both parts of this puzzle are complete! They provide two gold stars: **

At this point, you should [return to your Advent calendar](https://adventofcode.com/2022) and try another puzzle.

If you still want to see it, you can [get your puzzle input](https://adventofcode.com/2022/day/2/input).

## Tests

<!-- livebook:{"reevaluate_automatically":true} -->

```elixir
ExUnit.start(auto_run: false)

defmodule Day02Test do
  use ExUnit.Case, async: false

  setup_all do
    [
      input: "A Y\nB X\nC Z"
    ]
  end

  describe "Part1.score_lines/1" do
    test "returns expected value", %{input: input} do
      assert Day02.Part1.score_lines(input) == [8, 1, 6]
    end
  end

  describe "Part2.score_lines/1" do
    test "returns expected value", %{input: input} do
      assert Day02.Part2.score_lines(input) == [4, 1, 7]
    end
  end
end

ExUnit.run()
```

<!-- livebook:{"output":true} -->

```
..
Finished in 0.00 seconds (0.00s async, 0.00s sync)
2 tests, 0 failures

Randomized with seed 321774
```

<!-- livebook:{"output":true} -->

```
%{excluded: 0, failures: 0, skipped: 0, total: 2}
```
